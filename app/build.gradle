buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.24.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.oss.licenses.plugin'
apply plugin: 'io.fabric'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 1
def versionBuild = 5

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.2'
    defaultConfig {
        applicationId "com.alvinhkh.buseta"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        resConfigs 'en', 'zh-rHK'
        multiDexEnabled true
    }
    buildTypes {
        debug {
            versionNameSuffix "-build-${versionBuild}"
        }
        release {
            // shrinkResources true
            // minifyEnabled true
        }
    }
    buildTypes.each {
        it.resValue 'string', 'AD_BANNER_UNIT_ID', AdBannerUnitId
        it.resValue 'string', 'DIRECTION_API_KEY', DirectionApiKey
    }
    lintOptions {
        checkReleaseBuilds true
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/rxjava.properties'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    implementation('com.squareup.okhttp3:okhttp:3.8.1') {
        exclude group: 'org.json'
    }
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:cardview-v7:27.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta4'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:multidex:1.0.2'
    implementation 'com.android.support:preference-v14:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.android.support:support-annotations:27.0.2'
    implementation 'com.google.android.gms:play-services-location:11.8.0'
    implementation 'com.google.android.gms:play-services-maps:11.8.0'
    implementation 'com.google.android.gms:play-services-oss-licenses:11.8.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.google.firebase:firebase-ads:11.8.0'
    implementation 'com.google.firebase:firebase-appindexing:11.8.0'
    implementation 'com.google.firebase:firebase-config:11.8.0'
    implementation 'com.google.firebase:firebase-core:11.8.0'
    implementation 'com.google.firebase:firebase-firestore:11.8.0'
    implementation 'com.google.maps.android:android-maps-utils:0.5'
    implementation 'com.google.maps:google-maps-services:0.2.5'
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'
    implementation ('com.firebaseui:firebase-ui-firestore:3.1.2') {
        exclude group: 'com.google.firebase'
    }
    implementation (group: 'org.simpleframework', name: 'simple-xml', version: '2.7.1') {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'xpp3', module: 'xpp3'
    }
    implementation 'org.jsoup:jsoup:1.11.2'
    implementation 'com.github.chrisbanes:PhotoView:1.3.1'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation ('com.squareup.retrofit2:converter-gson:2.3.0') {
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    implementation ('com.squareup.retrofit2:converter-simplexml:2.3.0') {
        exclude group: 'org.simpleframework', module: 'simple-xml'
    }
    implementation 'com.jakewharton.timber:timber:4.6.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'org.osmdroid:osmdroid-android:5.6.5'
    implementation 'org.osgeo:proj4j:0.1.0'
    implementation 'org.slf4j:slf4j-nop:1.7.25'
    androidTestImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

apply plugin: 'com.google.gms.google-services'