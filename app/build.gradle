buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.2'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.oss.licenses.plugin'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'


def versionMajor = 1
def versionMinor = 0
def versionPatch = 2
def versionBuild = 0

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.alvinhkh.buseta"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        resConfigs 'en', 'zh-rHK'
        multiDexEnabled true
    }
    buildTypes {
        debug {
            versionNameSuffix "-build-${versionBuild}"
        }
        release {
            // shrinkResources true
            // minifyEnabled true
        }
    }
    buildTypes.each {
        it.resValue 'string', 'AD_BANNER_UNIT_ID', AdBannerUnitId
        it.resValue 'string', 'DIRECTION_API_KEY', DirectionApiKey
    }
    lintOptions {
        checkReleaseBuilds true
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/rxjava.properties'
    }
    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    implementation('com.squareup.okhttp3:okhttp:3.10.0') {
        exclude group: 'org.json'
    }
    implementation 'android.arch.lifecycle:runtime:1.1.1'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    annotationProcessor "android.arch.lifecycle:compiler:1.1.1"
    kapt "android.arch.lifecycle:compiler:1.1.1"
    implementation "android.arch.persistence.room:runtime:1.0.0"
    implementation "android.arch.persistence.room:rxjava2:1.0.0"
    annotationProcessor "android.arch.persistence.room:compiler:1.0.0"
    kapt "android.arch.persistence.room:compiler:1.0.0"
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.android.support:customtabs:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:preference-v14:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation 'com.google.android.gms:play-services-location:15.0.1'
    implementation 'com.google.android.gms:play-services-maps:15.0.1'
    implementation 'com.google.android.gms:play-services-oss-licenses:15.0.1'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.google.firebase:firebase-ads:15.0.1'
    implementation 'com.google.firebase:firebase-appindexing:15.0.1'
    implementation 'com.google.firebase:firebase-config:15.0.2'
    implementation 'com.google.firebase:firebase-core:15.0.2'
    implementation 'com.google.firebase:firebase-firestore:16.0.0'
    implementation 'com.google.maps.android:android-maps-utils:0.5'
    implementation 'com.google.maps:google-maps-services:0.2.7'
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'
    implementation ('com.firebaseui:firebase-ui-firestore:3.3.0') {
        exclude group: 'com.google.firebase'
    }
    implementation (group: 'org.simpleframework', name: 'simple-xml', version: '2.7.1') {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'xpp3', module: 'xpp3'
    }
    implementation 'org.jsoup:jsoup:1.11.2'
    implementation 'com.opencsv:opencsv:4.1'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation ('com.squareup.retrofit2:converter-gson:2.4.0') {
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    implementation ('com.squareup.retrofit2:converter-simplexml:2.4.0') {
        exclude group: 'org.simpleframework', module: 'simple-xml'
    }
    implementation 'com.jakewharton.timber:timber:4.7.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.12'
    implementation 'org.osmdroid:osmdroid-android:6.0.1'
    implementation 'org.osgeo:proj4j:0.1.0'
    implementation 'org.slf4j:slf4j-nop:1.7.25'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.core:core-ktx:0.3'
}

apply plugin: 'com.google.gms.google-services'